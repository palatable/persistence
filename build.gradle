apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'signing'

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
group = 'com.jnape.palatable'
version = '1.0'
description = "Persistent patterns that aren't antipatterns"
ext.packaging = 'jar'

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestCompile.extendsFrom compile, testCompile
    integrationTestRuntime.extendsFrom runtime, integrationTestCompile, testRuntime
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.3.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.3.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.3.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.4'
    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '2.13.3'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.2.3'

    integrationTestCompile group: 'com.github.fakemongo', name: 'fongo', version: '1.4.3'
    integrationTestCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.1'
}

idea {
    module {
        scopes.TEST.plus += configurations.integrationTestCompile
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'Persistence'
                packaging 'jar'
                description 'Persistence patterns that aren\'t anti-patterns'
                url 'http://github.com/palatable/persistence'

                scm {
                    url 'scm:git@github.com:palatable/persistence.git'
                    connection 'scm:git@github.com:palatable/persistence.git'
                    developerConnection 'scm:git@github.com:palatable/persistence.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://choosealicense.com/licenses/mit/'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jnape'
                        name 'John Napier'
                    }
                }
            }
        }
    }
}