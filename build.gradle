apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

sourceCompatibility = 1.8
group = 'com.jnape.palatable'
version = '1.0'

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestCompile.extendsFrom compile, testCompile
    integrationTestRuntime.extendsFrom runtime, integrationTestCompile, testRuntime
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath += sourceSets.integrationTest.runtimeClasspath
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.3.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.3.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.3.2'
    compile group: 'org.mongodb', name: 'mongo-java-driver', version: '2.11.4'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.12.6'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.2.3'

    integrationTestCompile group: 'com.github.fakemongo', name: 'fongo', version: '1.4.3'
    integrationTestCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.1.1'
}

idea {
    module {
        scopes.TEST.plus += configurations.integrationTestCompile
    }
}
